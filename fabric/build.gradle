import java.nio.file.Paths
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
}

loom {
    accessWidenerPath = file("src/main/resources/cheatutils.accesswidener")
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Uncomment the following line to enable the deprecated Fabric API modules. 
    // These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

    // modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

    annotationProcessor files('../mixin-plugin/annotation-processor/build/libs/annotation-processor.jar')
}

sourceSets {
    main {
        java {
            srcDir '../common/java'
            srcDir '../java-scripting-language/src/main/java'
            srcDir '../mixin-plugin/common/java'
        }
        resources {
            srcDir '../common/resources'
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(21)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }

    exclude("net/caffeinemc/**")
    exclude("net/irisshaders/**")
    exclude("com/zergatul/cheatutils/utils/mappings/**")
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add("-parameters")
}

tasks.register('buildAnnotationProcessor', {
    doFirst {
        def annotationProcessorJar = file('../mixin-plugin/annotation-processor/build/libs/annotation-processor.jar')
        if (!annotationProcessorJar.exists()) {
            println 'Annotation processor JAR not found. Building annotation processor...'
            def buildResult = exec {
                def gradlew = Os.isFamily(Os.FAMILY_WINDOWS) ? 'gradlew.bat' : 'gradlew'
                commandLine "../mixin-plugin/annotation-processor/$gradlew", '-p', '../mixin-plugin/annotation-processor', 'build'
            }
            if (buildResult.exitValue != 0) {
                throw new GradleException('Failed to build the annotation processor project.')
            }
        }
    }
})

tasks.withType(JavaCompile).configureEach {
    dependsOn('buildAnnotationProcessor')
}

tasks.build.doLast {
    def devJar = file(jar.archiveFile)
    // assuming loom set path to \fabric\build\devlibs\zergatul.cheatutils-{version}-dev.jar
    def finalJar = file(devJar.path.replace("devlibs", "libs").replace("-dev.", "."))
    def renamedJar = Paths.get(finalJar.parent, "$archives_base_name-$mod_version-fabric-${minecraft_version}.jar").toFile()

    if (finalJar.exists()) {
        if (renamedJar.exists()) {
            renamedJar.delete()
        }
        finalJar.renameTo(renamedJar)
    } else {
        println "Renaming final JAR failed"
        println "Can't find final JAR at: ${finalJar.path}"
    }
}